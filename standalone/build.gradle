plugins {
    id "java"
    id "application"
}

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

dependencies {
    include project(":impl-classtransform")

    include "net.sf.jopt-simple:jopt-simple:5.0.4"

    include "org.apache.logging.log4j:log4j-core:2.21.0"
    include "org.apache.logging.log4j:log4j-slf4j2-impl:2.21.1"

    compileOnly "org.jetbrains:annotations:24.0.1"

    include "me.tongfei:progressbar:0.9.5"

    include("org.jline:jline-terminal-jansi:3.23.0")
}

application {
    mainClass = "net.raphimc.javadowngrader.standalone.Main"
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }

    manifest {
        attributes(
                "Main-Class": application.mainClass,
                "Multi-Release": "true"
        )
    }
}

project.tasks.withType(PublishToMavenRepository).forEach {
    it.enabled = false
}
