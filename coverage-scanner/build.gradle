plugins {
    id "java"
    id "application"
}

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

dependencies {
    include api("org.ow2.asm:asm-commons:9.6")

    include "net.sourceforge.argparse4j:argparse4j:0.9.0"

    compileOnly "org.jetbrains:annotations:24.1.0"
}

application {
    mainClass = "net.raphimc.javadowngrader.coveragescanner.Main"
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude("META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA", "module-info.class")
    }

    manifest {
        attributes(
            "Main-Class": application.mainClass,
            "Multi-Release": "true"
        )
    }
}

project.tasks.withType(PublishToMavenRepository).forEach {
    it.enabled = false
}
