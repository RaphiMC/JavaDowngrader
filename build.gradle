plugins {
    id "maven-publish"
}

allprojects {
    apply plugin: "java-library"

    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    [compileJava, compileTestJava, javadoc].each {
        it.options.encoding = "UTF-8"
    }

    group = project.maven_group
    archivesBaseName = project.maven_name
    version = project.maven_version
    if (!project.maven_group) group = rootProject.maven_group
    if (!project.maven_name) archivesBaseName = rootProject.maven_name
    if (!project.maven_version) version = rootProject.maven_version

    repositories {
        mavenCentral()
    }
}

dependencies {
    api project(":runtime-dep")

    api "org.ow2.asm:asm-commons:9.5"
    api "org.slf4j:slf4j-api:2.0.7"
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            url = project.maven_version.endsWith("SNAPSHOT") ? "https://maven.lenni0451.net/snapshots" : "https://maven.lenni0451.net/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.maven_name
            version = project.maven_version

            from components.java
        }
    }
}
