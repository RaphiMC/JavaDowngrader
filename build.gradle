allprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    base {
        java.toolchain.languageVersion = JavaLanguageVersion.of(8)
        compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

        group = project.maven_group ?: rootProject.maven_group
        archivesName = project.maven_name ?: rootProject.maven_name
        version = project.maven_version ?: rootProject.maven_version
    }

    repositories {
        mavenCentral()
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.name ?: rootProject.name}" }
        }
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    publishing {
        repositories {
            maven {
                name = "reposilite"
                url = "https://maven.lenni0451.net/" + (project.maven_version.endsWith("SNAPSHOT") ? "snapshots" : "releases")

                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }

    signing {
        setRequired(false)
        sign configurations.archives
    }
}

configure(allprojects.findAll { it.name != "gradle-plugin" }) {
    publishing {
        repositories {
            maven {
                name = "ossrh"
                url = "https://s01.oss.sonatype.org/" + (project.maven_version.endsWith("SNAPSHOT") ? "content/repositories/snapshots/" : "service/local/staging/deploy/maven2/")

                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }

        publications {
            maven(MavenPublication) {
                groupId = project.maven_group
                artifactId = project.maven_name
                version = project.maven_version

                from components.java

                pom {
                    name = "JavaDowngrader"
                    description = "Standalone program and library which can downgrade Java classes/programs down to Java 8"
                    url = "https://github.com/RaphiMC/JavaDowngrader"
                    licenses {
                        license {
                            name = "LGPL-3.0 License"
                            url = "https://github.com/RaphiMC/JavaDowngrader/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "RK_01"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/RaphiMC/JavaDowngrader.git"
                        developerConnection = "scm:git:ssh://github.com/RaphiMC/JavaDowngrader.git"
                        url = "https://github.com/RaphiMC/JavaDowngrader.git"
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.maven
    }
}

allprojects {
    project.tasks.withType(PublishToMavenRepository).forEach {
        it.dependsOn(project.tasks.withType(Sign))
    }
}

dependencies {
    api project(":runtime-dep")

    api "org.ow2.asm:asm-commons:9.6"
}
